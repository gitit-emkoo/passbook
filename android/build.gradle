// Top-level build file where you can add configuration options common to all sub-projects/modules.

// 전역 ext 설정 (모든 서브프로젝트가 평가되기 전에 설정되어야 함)
// expo-modules-core가 평가되기 전에 반드시 설정되어야 함
// 모든 형태의 Kotlin 버전 속성을 확인
ext.kotlinVersion = findProperty('expo.kotlin.version') ?: findProperty('KOTLIN_VERSION') ?: findProperty('android.kotlinVersion') ?: findProperty('kotlinVersion') ?: '1.9.25'

buildscript {
    ext {
        buildToolsVersion = findProperty('android.buildToolsVersion') ?: '35.0.0'
        minSdkVersion = Integer.parseInt(findProperty('android.minSdkVersion') ?: '24')
        compileSdkVersion = Integer.parseInt(findProperty('android.compileSdkVersion') ?: '35')
        targetSdkVersion = Integer.parseInt(findProperty('android.targetSdkVersion') ?: '34')
        kotlinVersion = rootProject.ext.kotlinVersion

        ndkVersion = "26.1.10909125"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath('com.android.tools.build:gradle')
        classpath('com.facebook.react:react-native-gradle-plugin')
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
    }
}

plugins {
    id "org.jetbrains.kotlin.android" version "1.9.25" apply false
}

apply plugin: "com.facebook.react.rootproject"

allprojects {
    // 모든 서브프로젝트에서 kotlinVersion 접근 가능하도록 설정
    ext.kotlinVersion = rootProject.ext.kotlinVersion ?: '1.9.25'
    
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url(new File(['node', '--print', "require.resolve('react-native/package.json')"].execute(null, rootDir).text.trim(), '../android'))
        }
        maven {
            // Android JSC is installed from npm
            url(new File(['node', '--print', "require.resolve('jsc-android/package.json', { paths: [require.resolve('react-native/package.json')] })"].execute(null, rootDir).text.trim(), '../dist'))
        }

        google()
        mavenCentral()
        maven { url 'https://www.jitpack.io' }
    }
    configurations.configureEach {
        resolutionStrategy {
            force 'com.google.android.gms:play-services-ads:23.4.0',
                  'com.google.android.gms:play-services-ads-lite:23.4.0',
                  'com.google.android.gms:play-services-ads-base:23.4.0'
        }
    }
}
